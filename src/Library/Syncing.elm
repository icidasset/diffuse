module Syncing exposing (LocalConfig, RemoteConfig, task)

import Json.Decode as Decode
import Json.Encode as Json
import Maybe.Extra as Maybe
import Task exposing (Task)
import Task.Extra as Task
import Time
import Time.Ext as Time
import User.Layer as User exposing (..)



-- ğŸŒ³


type alias LocalConfig =
    { localData : HypaethralData
    , saveLocal : HypaethralBit -> Decode.Value -> Task String ()
    }


type alias RemoteConfig =
    { retrieve : HypaethralBit -> Task String (Maybe Decode.Value)
    , save : HypaethralBit -> Decode.Value -> Task String ()
    }



-- ğŸ› 


{-| Syncs all hypaethral data.

Returns `Nothing` if the local data is preferred.

ğŸï¸ LOCAL
ğŸ›°ï¸ REMOTE

1.  Try to pull remote `modified.json` timestamp
    a. If newer, continue (#2)
    b. If same, do nothing
    c. If older, or not present, prefer local data ğŸï¸ (stop & push)
2.  Try to download all remote data
    a. If any remote data, continue (#3)
    b. If none, prefer local data ğŸï¸ (stop & push)
3.  Decode remote data and compare timestamps
    a. If newer, use remote data ğŸ›°ï¸
    b. If same, do nothing
    c. If older, prefer local data ğŸï¸ (stop & push)
    d. If no timestamps, if local data, prefer local ğŸï¸ (stop & push), otherwise remote ğŸ›°ï¸

-}
task :
    Task String a
    -> LocalConfig
    -> RemoteConfig
    -> Task String (Maybe HypaethralData)
task initialTask localConfig remoteConfig =
    initialTask
        |> Task.andThen
            (\_ ->
                remoteConfig.retrieve ModifiedAt
            )
        |> Task.andThen
            (\maybeModifiedAt ->
                let
                    maybeRemoteModifiedAt =
                        Maybe.andThen
                            (Decode.decodeValue Time.decoder >> Result.toMaybe)
                            maybeModifiedAt
                in
                case ( maybeRemoteModifiedAt, localConfig.localData.modifiedAt ) of
                    ( Just remoteModifiedAt, Just localModifiedAt ) ->
                        if Time.posixToMillis remoteModifiedAt == Time.posixToMillis localModifiedAt then
                            -- ğŸï¸
                            Task.succeed Nothing

                        else if Time.posixToMillis remoteModifiedAt > Time.posixToMillis localModifiedAt then
                            -- ğŸ›°ï¸
                            fetchRemote localConfig remoteConfig

                        else
                            -- ğŸï¸ â†’ ğŸ›°ï¸
                            pushLocalToRemote localConfig remoteConfig { return = Nothing }

                    ( Just _, Nothing ) ->
                        -- ğŸ›°ï¸
                        fetchRemote localConfig remoteConfig

                    ( Nothing, _ ) ->
                        -- ğŸ›°ï¸
                        fetchRemote localConfig remoteConfig
            )


fetchRemote :
    LocalConfig
    -> RemoteConfig
    -> Task String (Maybe HypaethralData)
fetchRemote localConfig remoteConfig =
    let
        { localData, saveLocal } =
            localConfig

        { retrieve } =
            remoteConfig

        saveLocally data =
            data
                |> User.saveHypaethralData saveLocal
                |> Task.map (\_ -> Just data)

        noLocalData =
            List.isEmpty localData.sources
                && List.isEmpty localData.favourites
                && List.isEmpty localData.playlists
    in
    retrieve
        |> User.retrieveHypaethralData
        |> Task.andThen
            (\list ->
                let
                    remoteHasExistingData =
                        List.any (Tuple.second >> Maybe.isJust) list
                in
                if remoteHasExistingData then
                    -- ğŸ›°ï¸
                    Task.succeed list

                else
                    -- ğŸï¸ â†’ ğŸ›°ï¸
                    pushLocalToRemote localConfig remoteConfig { return = list }
            )
        |> Task.andThen
            (\list ->
                -- Decode remote
                list
                    |> List.map (\( a, b ) -> ( hypaethralBitKey a, Maybe.withDefault Json.null b ))
                    |> Json.object
                    |> User.decodeHypaethralData
                    |> Task.fromResult
                    |> Task.mapError Decode.errorToString
            )
        |> Task.andThen
            (\remoteData ->
                -- Compare modifiedAt timestamps
                case ( remoteData.modifiedAt, localData.modifiedAt ) of
                    ( Just remoteModifiedAt, Just localModifiedAt ) ->
                        if Time.posixToMillis remoteModifiedAt == Time.posixToMillis localModifiedAt then
                            -- ğŸï¸
                            Task.succeed Nothing

                        else if Time.posixToMillis remoteModifiedAt > Time.posixToMillis localModifiedAt then
                            -- ğŸ›°ï¸
                            saveLocally remoteData

                        else
                            -- ğŸï¸ â†’ ğŸ›°ï¸
                            pushLocalToRemote localConfig remoteConfig { return = Nothing }

                    ( Just _, Nothing ) ->
                        -- ğŸ›°ï¸
                        saveLocally remoteData

                    ( Nothing, Just _ ) ->
                        -- ğŸï¸ â†’ ğŸ›°ï¸
                        pushLocalToRemote localConfig remoteConfig { return = Nothing }

                    _ ->
                        if noLocalData then
                            -- ğŸ›°ï¸
                            saveLocally remoteData

                        else
                            -- ğŸï¸
                            Task.succeed Nothing
            )



-- ãŠ™ï¸


pushLocalToRemote : LocalConfig -> RemoteConfig -> { return : a } -> Task String a
pushLocalToRemote localConfig remoteConfig { return } =
    localConfig.localData
        |> User.encodedHypaethralDataList
        |> List.map (\( bit, data ) -> remoteConfig.save bit data)
        |> Task.sequence
        |> Task.map (\_ -> return)
